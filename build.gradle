buildscript {
    repositories {
        maven {
            url 'http://artifactory.morethanheroic.com/artifactory'
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
    }

    dependencies {
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: '1.4.0.RELEASE'
    }
}

plugins {
    id "com.jfrog.artifactory" version "4.0.0"
}

allprojects {
    apply plugin: 'java'

    artifactory {
        contextUrl = "http://artifactory.morethanheroic.com/artifactory"
        publish {
            repository {
                repoKey = 'libs-release-local'
                username = "${artifactory_user}"
                password = "${artifactory_password}"
                maven = true
            }
            defaults {
                publications('mavenJava')
            }
        }
        resolve {
            repository {
                repoKey = 'libs-release'
                username = "${artifactory_user}"
                password = "${artifactory_password}"
                maven = true
            }
        }
    }

    apply plugin: 'maven-publish'
    apply plugin: "com.jfrog.artifactory"

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    group = 'com.morethanheroic.swords'
    version = '0.0.1'

    ext {
        mybatis_version = '3.4.0'
        guava_version = '18.0'
        checkstyle_version = '6.13'
        findbugs_version = '3.0.1'
        commons_lang_version = '3.0.1'
        jackson_version = '2.6.4'

        checkstyle_config = {
            toolVersion = project.ext.checkstyle_version
            configFile = file("${rootProject.rootDir}/config/checkstyle/checkstyle.xml");
        }

        findbugs_config = {
            toolVersion = project.ext.findbugs_version
            effort = "max"
            reportLevel = "medium"
        }
    }

    dependencies {
        compile group: 'org.projectlombok', name: 'lombok', version: '1.16.6'
        compile group: 'com.google.guava', name: 'guava', version: guava_version

        compile group: 'com.morethanheroic.swords', name: 'swords-datapack', version: '0.0.131'
    }

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    compile(group: 'org.springframework.boot', name: 'spring-boot-starter') {
        exclude(module: 'commons-logging')
    }
    compile group: 'org.springframework.security', name: 'spring-security-core'

    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test') {
        exclude(module: 'commons-logging')
    }

    compile group: 'org.logback-extensions', name: 'logback-ext-loggly', version: '0.1.4'

    compile group: 'org.apache.commons', name: 'commons-math3', version: '3.5'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'

    compile group: 'org.liquibase', name: 'liquibase-core'
    compile group: 'mysql', name: 'mysql-connector-java'
    compile group: 'org.mybatis', name: 'mybatis-spring', version: '1.3.0'
    compile group: 'org.mybatis', name: 'mybatis', version: mybatis_version

    compile group: 'com.morethanheroic', name: 'common-session', version: '1.4.13'
    compile group: 'com.morethanheroic', name: 'common-response', version: '1.0.0'
    compile group: 'com.morethanheroic', name: 'common-application', version: '1.0.7'

    testCompile group: 'org.testng', name: 'testng', version: '6.9.9'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'

    compile project(':xml-loader')
    compile project(':common-memoize')
    compile project(':common-validator')
    compile project(':common-sql')
    compile project(':common-dependency-injection')

    compile project(':swords-equipment')
    compile project(':swords-user')
    compile project(':swords-response')
    compile project(':swords-skill')
    compile project(':swords-skill-scavenging')
    compile project(':swords-race')
    compile project(':swords-effect')
    compile project(':swords-web')
    compile project(':swords-money')
    compile project(':swords-item')
    compile project(':swords-monster')
    compile project(':swords-journal')
    compile project(':swords-attribute')
    compile project(':swords-session')
    compile project(':swords-inventory')
    compile project(':swords-registration')
    compile project(':swords-combat')
    compile project(':swords-settings')
}

apply plugin: 'application'
mainClassName = 'com.morethanheroic.swords.SwordsorceryServerApplication'

apply plugin: 'spring-boot'

task stage(dependsOn: ['clean', 'installApp'])